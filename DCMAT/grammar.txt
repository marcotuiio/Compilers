Start -> Comandos EOL
Start -> Expressao EOL
Start -> ExpressaoMultiplicativa

Comandos -> SHOW SETTINGS SEMICOLON
Comandos -> RESET SETTINGS SEMICOLON
Comandos -> SET H_VIEW Expressao COLON Expressao SEMICOLON
Comandos -> SET V_VIEW Expressao COLON Expressao SEMICOLON
Comandos -> SET AXIS ON SEMICOLON
Comandos -> SET AXIS OFF SEMICOLON
Comandos -> PLOT SEMICOLON
Comandos -> PLOT ( ExpressaoAditiva ) SEMICOLON
Comandos -> SET ERASE PLOT OFF SEMICOLON
Comandos -> SET ERASE PLOT ON SEMICOLON
Comandos -> RPN ( Expressao ) SEMICOLON
Comandos -> SET INTEGRAL_STEPS Expressao SEMICOLON
Comandos -> INTEGRATE ( Expressao COLON Expressao COMMA ExpressaoAditiva ) SEMICOLON
Comandos -> SUM ( ID COMMA Expressao COLON Expressao COMMA ExpressaoAditiva ) SEMICOLON

Comandos -> Token_Matrix ASSIGN [ [ NUM_FLOAT Repet_Matrix ] Repet_Dimen ] SEMICOLON
Token_Matrix -> MATRIX
Repet_Matrix -> COMMA NUM_FLOAT Repet_Matrix SEMICOLON
Repet_Matrix ->
Repet_Dimen -> COMMA [ NUM_FLOAT Repet_Matrix ] Repet_Dimen
Repet_Dimen ->

Comandos -> SHOW MATRIX SEMICOLON
Comandos -> SOLVE DETERMINANT SEMICOLON
Comandos -> SOLVE LINEAR_SYSTEM SEMICOLON
Comandos -> ABOUT SEMICOLON

Comandos -> ID COLON_ASSIGN Expressao SEMICOLON
Comandos ->  Token_Matrix_Id Expressao Repet_Matrix ] Repet_Dimen ] SEMICOLON
Token_Matrix_Id -> ID COLON_ASSIGN [ [
Comandos -> ID SEMICOLON
Comandos -> SHOW SYMBOLS SEMICOLON

Comandos -> SET FLOAT PRECISION Expressao SEMICOLON
Comandos -> SET CONNECT_DOTS ON SEMICOLON
Comandos -> SET CONNECT_DOTS OFF SEMICOLON

Comandos -> QUIT

Funcao -> SEN ( ExpressaoAditiva )
Funcao -> COS ( ExpressaoAditiva )
Funcao -> TAN ( ExpressaoAditiva )
Funcao -> ABS ( ExpressaoAditiva )

Expressao -> ExpressaoAditiva

OpUnario -> +
OpUnario -> -

OpMult -> *
OpMult -> /
OpMult -> ^
OpMult -> %

ExpressaoAditiva -> ExpressaoMultiplicativa
ExpressaoAditiva -> ExpressaoAditiva OpUnario ExpressaoMultiplicativa

ExpressaoMultiplicativa -> ExpressaoUnaria
ExpressaoMultiplicativa -> ExpressaoMultiplicativa OpMult ExpressaoUnaria

ExpressaoUnaria -> ExpressaoPrimaria
ExpressaoUnaria -> OpUnario ExpressaoUnaria

ExpressaoPrimaria -> ID
ExpressaoPrimaria -> VAR_X
ExpressaoPrimaria -> NUM_INT
ExpressaoPrimaria -> NUM_FLOAT
ExpressaoPrimaria -> PI
ExpressaoPrimaria -> EULER
ExpressaoPrimaria -> ( ExpressaoAditiva )
ExpressaoPrimaria -> Funcao
